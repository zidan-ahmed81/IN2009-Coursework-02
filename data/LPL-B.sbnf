-- Note: some tokens are defined to reserve them for
-- future language extensions but are not used in the current
-- LPL grammar

tokens {
    SKIP: "((//.*(\r?\n))|\s)+" -- DO NOT MODIFY (simple comments and whitespace)
    LSQBR: "\["
    RSQBR: "\]"
    DOT: "\."
    LBR: "\("
    RBR: "\)"
    LCBR: "\{"
    RCBR: "\}"
    SEMIC: ";"
    COMMA: ","
    COLON: ":"
    BEGIN: "begin"
    END: "end"
    NEW: "new"
    LENGTH: "length"
    PRINTLN: "println"
    PRINT: "print"
    PRINTCH: "printch"
    READINT: "readint"
    NEWLINE: "newline"
    INT_TYPE: "int"
    NULL: "null"
    ASSIGN: "="
    ARROW: "->"
    PROC: "proc"
    FUN: "fun"
    WHILE: "while"
    IF: "if"
    ELSE: "else"
    RETURN: "return"
    SWITCH: "switch"
    CASE: "case"
    DEFAULT: "default"
    MUL: "\*"
    DIV: "/"
    ADD: "\+"
    MINUS: "-"
    EQ: "=="
    LT: "<"
    LE: "<="
    NOT: "!"
    AND: "&&"
    OR: "\|\|"
    STRINGLIT: ""[^\\"\n]*("|\\([^\n][^\\"\n]*\\)*[^\n][^\\"\n]*")"
    ID: "(_[a-zA-Z_0-9$]+)|([a-zA-Z][a-zA-Z_0-9$]*)"
    -- integer literals (unsigned)
    INTLIT: "[0-9]+"
}

Program -> BEGIN NonFormalVarDecl* Stm* END FunOrProcDef*

NonFormalVarDecl -> Type ID SEMIC

Type -> INT_TYPE

FunOrProcDef -> FUN Type MethodDef
FunOrProcDef -> PROC MethodDef

MethodDef -> ID LBR Formals RBR LCBR NonFormalVarDecl* Stm* RCBR

Formals -> Type ID AnotherFormal*
Formals ->

AnotherFormal -> COMMA Type ID

Stm -> ID StmIdFactor
Stm -> IF LBR Exp RBR Stm ELSE Stm -- if-statement
Stm -> WHILE LBR Exp RBR Stm -- while-loop
Stm -> PRINT Exp SEMIC -- output decimal (no newline)
Stm -> PRINTLN Exp SEMIC -- output decimal (with newline)
Stm -> PRINTCH Exp SEMIC -- output character
Stm -> NEWLINE SEMIC -- output newline
Stm -> RETURN StmReturnFactor
Stm -> LCBR Stm* RCBR -- block
Stm -> SWITCH LBR Exp RBR LCBR Case* DEFAULT COLON Stm RCBR

Case -> CASE OptionalSign INTLIT COLON Stm

OptionalSign -> MINUS
OptionalSign ->

StmIdFactor -> LBR Actuals RBR SEMIC -- method call
StmIdFactor -> ASSIGN Exp SEMIC -- assignment to a variable

StmReturnFactor -> Exp SEMIC -- return from function call
StmReturnFactor -> SEMIC -- return from procedure call (or halt in main program)

Exp -> SimpleExp OperatorClause

OperatorClause -> Operator SimpleExp
OperatorClause ->

SimpleExp -> ID SimpleIdFactor
SimpleExp -> OptionalSign INTLIT -- integer literal
SimpleExp -> NOT SimpleExp -- logical negation
SimpleExp -> LBR Exp RBR

SimpleIdFactor -> LBR Actuals RBR -- function call
SimpleIdFactor -> -- dereference a variable

Operator -> MUL
Operator -> DIV
Operator -> MINUS
Operator -> ADD
Operator -> LT
Operator -> LE
Operator -> EQ
Operator -> AND
Operator -> OR

Actuals -> Exp AnotherActual*
Actuals ->

AnotherActual -> COMMA Exp
